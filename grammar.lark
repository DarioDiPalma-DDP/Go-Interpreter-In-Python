?statement: declaration
          | bool_logic
          | if_stmt
          | for_stmnt
          | IDENTIFIER "(" expression ")" -> call

block_statement: "{" statement* "}"

?bool_logic: relation
          | "(" bool_logic ")"
          | bool_logic "&&" bool_logic -> bool_and
          | bool_logic "||" bool_logic -> bool_or
          | "!" bool_logic -> bool_not

?relation: "(" relation ")"
        | expression "<" expression -> less
        | expression ">" expression -> great
        | expression ">=" expression -> great_eq
        | expression "<=" expression -> less_eq
        | expression "==" expression -> equal
        | expression "!=" expression -> not_equal

expression: term
          | expression "+" expression -> addition
          | expression "-" expression -> subtraction

term: factor
    | factor "*" factor -> multiplication
    | factor "/" factor -> division

factor: NUMBER
      | IDENTIFIER
      | STRING

// Variable declarations
declaration: var_declare
           | short_declare

// Supported type
DATA_TYPE: "int" | "string" | "bool"

// var x = 0 || var b,c int = 1,3
var_declare: "var" ident_list (DATA_TYPE ("=" expression_list)? | "=" expression_list) -> assignment
ident_list: IDENTIFIER ("," IDENTIFIER)*
expression_list: expression ("," expression)*

// x := 2
short_declare: ident_list ":=" expression_list -> short_assignment

// If Statement
if_stmt: "if" bool_logic block_statement ("else" (if_stmt|block_statement))?

// For Statement
for_stmnt: "for" bool_logic block_statement -> while_loop
         | "for" short_declare ";" bool_logic ";" IDENTIFIER "++" block_statement -> for_loop

// String type, enclosed by double quotes (ex. "ciao")
STRING : /".*?(?<!\\)"/

// Import terminals from common library
%import common.CNAME -> IDENTIFIER
%import common.NUMBER
%import common.WS

%ignore WS